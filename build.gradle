plugins {
    id "org.jetbrains.kotlin.jvm" version "1.8.10" apply false
}

allprojects {
    apply plugin: "kotlin"
    apply plugin: "maven-publish"

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    archivesBaseName = project.archives_base_name
    version = project.mod_version
    group = project.maven_group

    repositories {
        mavenCentral()

        maven {
            url "https://repo.essential.gg/repository/maven-public"
        }
    }
    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release.set(17)
    }

    java {
        withSourcesJar()
    }

    jar {
        from("LICENSE") {
            rename { "${it}_${project.archivesBaseName}" }
        }

        exclude "dev/chachy/editor/api/mouse/**"
    }

    compileKotlin.kotlinOptions.jvmTarget = "17"

    publishing {
        publications {
            //noinspection GroovyAssignabilityCheck
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    description.set("Simplifies component configuration in Elementa")
                    url.set("https://github.com/ChachyDev/elementa-component-editor")

                    licenses {
                        license {
                            name.set("GNU Lesser General Public License v3.0")
                            url.set("https://github.com/ChachyDev/elementa-component-editor/blob/main/LICENSE")
                        }
                    }
                    developers {
                        developer {
                            id.set("chachy")
                            name.set("ChachyDev")
                            email.set("shaloma123@protonmail.com")
                        }
                    }
                    scm {
                        connection.set("scm:git:git://github.com/ChachyDev/elementa-component-editor.git")
                        developerConnection.set("scm:git:ssh://github.com/ChachyDev/elementa-component-editor.git")
                        url.set("https://github.com/ChachyDev/elementa-component-editor/")
                    }
                }
            }
        }
    }
}